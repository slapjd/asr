# Patched by slapjd, everything else done by other contributors
# Maintainer: slapjd <slapjd@outlook.com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=libx11-slapjd-git
pkgver=1.8.9.r0.ga4655882
pkgrel=1
pkgdesc="Core X11 protocol client library"
arch=('i686' 'x86_64')
url="https://www.x.org/wiki/"
license=('custom')
depends=('glibc' 'libxcb' 'xorgproto')
makedepends=('git' 'xorg-util-macros' 'xtrans')
provides=("libx11=$pkgver" "libx11-git=$pkgver")
conflicts=('libx11' 'libx11-git')
options=('staticlibs')
source=(
  "git+https://gitlab.freedesktop.org/xorg/lib/libx11.git"
  "colemak-mod.patch"
)
sha256sums=(
  'SKIP'
  'e924a06556bf9d965d922152d4d4564e7b424672b9cff854e45d72f0e693f436'
)


pkgver() {
  cd "libx11"

  git describe --long --tags | sed 's/^libX11-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "libx11"
  patch -Np1 < ../colemak-mod.patch

  NOCONFIGURE=1 ./autogen.sh
  ./configure \
    --prefix="/usr" \
    --disable-xf86bigfont
  make
}

check() {
  cd "libx11"

  make check
}

package() {
  cd "libx11"

  make DESTDIR="$pkgdir" install
  install -Dm644 "COPYING" -t "$pkgdir/usr/share/licenses/libx11"
}
