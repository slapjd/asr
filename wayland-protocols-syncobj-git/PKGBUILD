# Patched by slapjd, everything else done by other contributors
# Maintainer: slapjd <slapjd@outlook.com>
# Contributor: Gennadiy Mykhailiuta <gmykhailiuta@gmail.com>
# Contributor: Emmanuel Gil Peyrot <emmanuel.peyrot@collabora.com>

_pkgname=wayland-protocols
pkgname="${_pkgname}-syncobj-git"
pkgver=1.33.r2.g4c8840c
pkgrel=1
pkgdesc='Wayland protocols that add functionalities not available in the core protocol'
arch=('any')
url='https://wayland.freedesktop.org/'
license=('MIT')
makedepends=('git' 'wayland' 'meson' 'ninja')
conflicts=("${_pkgname}" "${_pkgname}-git")
provides=("${_pkgname}" "${_pkgname}-git")

source=(
    "git+https://gitlab.freedesktop.org/wayland/wayland-protocols.git"
    "90.patch::https://gitlab.freedesktop.org/wayland/wayland-protocols/-/merge_requests/90.patch"
)
sha1sums=(
    'SKIP'
    'SKIP'
)

pkgver() {
    cd "$_pkgname"
    git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    mkdir -p "${_pkgname}/build"
    cd "${_pkgname}"
    patch -Np1 < "$srcdir/90.patch"
}

build() {
    meson build "${_pkgname}" --buildtype=release --prefix=/usr
    ninja -C build
}

check() {
    ninja -C build test
}

package() {
    DESTDIR="$pkgdir" ninja -C build install
    install -Dt "$pkgdir/usr/share/licenses/${_pkgname}" -m 644 "$_pkgname/COPYING"
}
