# Patched by slapjd, everything else done by other contributors
# Maintainer: slapjd <slapjd@outlook.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens <jan.steffens@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mladen Pejakovic <pejakm@gmail.com>

pkgbase=libxkbcommon-alt-git
pkgname=(
  libxkbcommon-alt-git
  libxkbcommon-x11-alt-git
)
pkgver=1.6.0_r2257.gabb6e588
pkgrel=1
pkgdesc='Keymap handling library for toolkits and window systems'
url='https://xkbcommon.org/'
arch=(x86_64)
license=(custom)
makedepends=(git libxcb libxml2 meson wayland wayland-protocols xkeyboard-config xorgproto)
checkdepends=(libgl xorg-server-xvfb)
options=(
  debug
)
source=("git+https://github.com/xkbcommon/libxkbcommon")
sha256sums=('SKIP')

pkgver() {
  cd libxkbcommon
  _ver="$(grep -m1 'version:' meson.build | cut -d "'" -f2 | tr - .)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  cd libxkbcommon
}

build() {
  local meson_options=(
    --libdir /usr/lib/alt
    -D enable-bash-completion=false
    -D enable-docs=false
    -D enable-tools=false
  )

  arch-meson libxkbcommon build "${meson_options[@]}"
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_libxkbcommon-alt-git() {
  depends=(libxml2 xkeyboard-config libxkbcommon)
  optdepends=(
    'libxkbcommon-x11: xkbcli interactive-x11'
    'wayland: xkbcli interactive-wayland'
  )

  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir/usr/include"

  _pick x11 "$pkgdir"/usr/lib/alt/*x11*
  _pick x11 "$pkgdir"/usr/lib/alt/pkgconfig/*x11*

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 libxkbcommon/LICENSE
}

package_libxkbcommon-x11-alt-git() {
  pkgdesc='Keyboard handling library using XKB data for X11 XCB clients'
  depends=(libxcb libxkbcommon libxkbcommon.so libxkbcommon-x11)

  mv x11/* "$pkgdir"/

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 libxkbcommon/LICENSE
}

# vim:set sw=2 sts=-1 et:
